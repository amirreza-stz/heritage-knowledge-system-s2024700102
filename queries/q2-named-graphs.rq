# Query 2: Find conflicting claims (same ritual, different cycles) - Named Graphs Pattern
# Question 2: Multi-Perspective Knowledge Representation
# 
# This query finds conflicting claims where the same ritual is claimed
# to belong to different cycles by different sources.
# Uses GRAPH keyword to query named graphs.

PREFIX : <http://www.semanticweb.org/ubuntu/ontologies/2026/0/s2024700102heritage/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?ritual ?ritualLabel ?cycle1 ?cycle1Label ?source1 ?source1Label ?confidence1 ?cycle2 ?cycle2Label ?source2 ?source2Label ?confidence2
WHERE {
    GRAPH ?graph1 {
        ?ritual :belongsTo ?cycle1 .
        ?graph1 :source ?source1 ;
                :confidence ?confidence1 .
    }
    ?ritual rdfs:label ?ritualLabel .
    FILTER(LANG(?ritualLabel) = "en" || LANG(?ritualLabel) = "")
    ?cycle1 rdfs:label ?cycle1Label .
    FILTER(LANG(?cycle1Label) = "en" || LANG(?cycle1Label) = "")
    ?source1 rdfs:label ?source1Label .
    FILTER(LANG(?source1Label) = "en" || LANG(?source1Label) = "")
    
    GRAPH ?graph2 {
        ?ritual :belongsTo ?cycle2 .
        ?graph2 :source ?source2 ;
                :confidence ?confidence2 .
    }
    ?cycle2 rdfs:label ?cycle2Label .
    FILTER(LANG(?cycle2Label) = "en" || LANG(?cycle2Label) = "")
    ?source2 rdfs:label ?source2Label .
    FILTER(LANG(?source2Label) = "en" || LANG(?source2Label) = "")
    
    FILTER (?graph1 != ?graph2 && ?cycle1 != ?cycle2)
}
ORDER BY ?ritualLabel
