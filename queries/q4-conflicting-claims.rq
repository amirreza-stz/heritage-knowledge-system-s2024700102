# Query 4: Find conflicting claims (same ritual, different cycles)
# Works for all three patterns - adjust based on dataset

PREFIX ex: <http://example.org/heritage/s2024700102#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

# For RDF Reification pattern:
SELECT ?ritual ?cycle1 ?source1 ?cycle2 ?source2 ?confidence1 ?confidence2
WHERE {
    ?claim1 a rdf:Statement ;
        rdf:subject ?ritual ;
        rdf:predicate ex:belongsTo ;
        rdf:object ?cycle1 ;
        ex:source ?source1 ;
        ex:confidence ?confidence1 .
    ?claim2 a rdf:Statement ;
        rdf:subject ?ritual ;
        rdf:predicate ex:belongsTo ;
        rdf:object ?cycle2 ;
        ex:source ?source2 ;
        ex:confidence ?confidence2 .
    FILTER (?cycle1 != ?cycle2)
    FILTER (?source1 != ?source2)
}
ORDER BY ?ritual

# For Named Graphs pattern, use:
# GRAPH ?g1 { ?ritual ex:belongsTo ?cycle1 . }
# GRAPH ?g2 { ?ritual ex:belongsTo ?cycle2 . }
# FILTER (?g1 != ?g2)

# For RDF-star pattern, use:
# << ?ritual ex:belongsTo ?cycle1 >> ex:source ?source1 .
# << ?ritual ex:belongsTo ?cycle2 >> ex:source ?source2 .
# FILTER (?cycle1 != ?cycle2)
